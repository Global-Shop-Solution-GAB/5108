Program.Sub.Preflight.Start

Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.Try
	
	V.Local.sError.Declare(String,"")
	V.Local.sHook.Declare(String,"")
	
	F.ODBC.Connection!con.OpenCompanyConnection
	
	V.Local.sHook.Set(V.Caller.Hook)
	
	F.Intrinsic.Control.SelectCase(V.Local.sHook)
		'Hook 12010 is the Populate hook
		F.Intrinsic.Control.Case("12010")
			F.Intrinsic.Control.CallSub(populate)
			
		'Hook 12705 is the Line Change Hook
		F.Intrinsic.Control.Case("12705")
			F.Intrinsic.Control.CallSub(entrycheck)
			
		'Hook 12435 is the Change Checkbox hook
		F.Intrinsic.Control.Case("12435")
			F.Intrinsic.Control.CallSub(clickstart)
			
		'Hook 12231 is the post save hook
		F.Intrinsic.Control.Case("12231")
			F.Intrinsic.Control.CallSub(saveentry)
			
	F.Intrinsic.Control.EndSelect
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: GCG_5108_SOLnApproval.g2u{0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(unload)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.unload.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	
	F.ODBC.Connection!con.Close
	F.Intrinsic.Control.End

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: GCG_5108_SOLnApproval.g2u{0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndTry
Program.Sub.unload.End

Program.Sub.Populate.Start
F.Intrinsic.Control.Try

	V.Local.sError.Declare(String,"")

	'This subroutine makes the custom checkbox and label visible to the user
	
	'FOR LABEL
	'Set horizontal position
	f.Intrinsic.Variable.SetProperty("GAB-LBL-1","PL",384)
	'Set vertical position
	F.Intrinsic.Variable.SetProperty("GAB-LBL-1","PT",8)
	f.Intrinsic.UI.ChangeCallerProperty(v.Passed.GAB-LBL-1,"HIDE",0)
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-1,"LOCK",0)
	V.Passed.GAB-LBL-1.Set("Approved")
	
	'FOR TEXT BOX
	'Set horizontal position
	f.Intrinsic.Variable.SetProperty("GAB-CHK-1","PL",364)
	'Set vertical position
	F.Intrinsic.Variable.SetProperty("GAB-CHK-1","PT",8)
	f.Intrinsic.UI.ChangeCallerProperty(v.Passed.GAB-CHK-1,"HIDE",0)
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-CHK-1,"LOCK",0)
	
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.000061, "LOCK", 1)
	V.Passed.GAB-CHK-1.Set("N")
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: GCG_5108_SOLnApproval.g2u{0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(unload)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndTry
Program.Sub.Populate.End

Program.Sub.EntryCheck.Start
F.Intrinsic.Control.Try

	V.Local.sError.Declare(String)
	V.Local.sOrdNumb.Declare(String)
	V.Local.sLine.Declare(String)
	V.Local.sSql.Declare(String)
	
	'This subroutine will set the custom checkbox and Lock/unlock the Gen Wo button according to the data saved in the table
	
	F.Intrinsic.String.LPad(V.Passed.000060, "0", 7, V.Local.sOrdNumb)
	F.Intrinsic.String.RPad(V.Passed.000051, "0", 2, V.Local.sLine)
	F.Intrinsic.String.LPad(V.Local.sLine, "0", 4, V.Local.sLine)
	
	F.Intrinsic.String.Build("Select APPROVAL_FLAG from SO_LINE_APPROVAL where ORDER_NO = '{0}' and LINE = '{1}' ", V.Local.sOrdNumb, V.Local.sLine, V.Local.sSql)
	F.ODBC.Connection!con.OpenLocalRecordsetRO("rstCheck", V.Local.sSql)
	F.Intrinsic.Control.If(V.ODBC.con!rstCheck.EOF, =, False)
		F.Intrinsic.Control.If(V.ODBC.con!rstCheck.FieldValTrim!APPROVAL_FLAG, =, True)
			F.Intrinsic.UI.ChangeCallerProperty(V.Passed.000061, "LOCK", 0)
			V.Passed.GAB-CHK-1.Set("Y")
			F.ODBC.con!rstCheck.Close
			F.Intrinsic.Control.ExitSub
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.000061, "LOCK", 1)
	V.Passed.GAB-CHK-1.Set("N")
	
	F.ODBC.con!rstCheck.Close
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: GCG_5108_SOLnApproval.g2u{0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(unload)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndTry
Program.Sub.EntryCheck.End

Program.Sub.ClickStart.Start
F.Intrinsic.Control.Try

	V.Local.sError.Declare(String)

	'This subroutine will lock/unlock the GEN WO button when the checkbox is checked/unchecked
	
	'Gen WO button control is passed.000061. The checkbox saves Y for checked and N for unchecked.
	F.Intrinsic.Control.If(Variable.Passed.GAB-CHK-1, =, "Y")
		F.Intrinsic.UI.ChangeCallerProperty(V.Passed.000061, "LOCK", 0)
	F.Intrinsic.Control.Else
		F.Intrinsic.UI.ChangeCallerProperty(V.Passed.000061, "LOCK", 1)
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: GCG_5108_SOLnApproval.g2u{0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(unload)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndTry
Program.Sub.ClickStart.End

Program.Sub.SaveEntry.Start
F.Intrinsic.Control.Try

	V.Local.sError.Declare(String)
	V.Local.sOrdNumb.Declare(String)
	V.Local.sLine.Declare(String)
	V.Local.sSql.Declare(String)
	V.Local.sFlag.Declare(String)
	
	'This subroutine will either update the APPROVAL_FLAG in an existing entry or create a new entry in the SO_LINE_APPROVAL table in the post save hook
	
	F.Intrinsic.String.LPad(V.Passed.000060, "0", 7, V.Local.sOrdNumb)
	F.Intrinsic.String.RPad(V.Passed.000051, "0", 2, V.Local.sLine)
	F.Intrinsic.String.LPad(V.Local.sLine, "0", 4, V.Local.sLine)
	
	F.Intrinsic.Control.If(V.Passed.GAB-CHK-1.Trim, =, "Y")
		V.Local.sFlag.Set("1")
	F.Intrinsic.Control.Else
		V.Local.sFlag.Set("0")
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.String.Build("Select * from SO_LINE_APPROVAL where ORDER_NO = '{0}' and LINE = '{1}' ", V.Local.sOrdNumb, V.Local.sLine, V.Local.sSql)
	F.ODBC.Connection!con.OpenLocalRecordsetRW("rstCheck", V.Local.sSql)
	F.Intrinsic.Control.If(V.ODBC.con!rstCheck.EOF, =, False)
		F.ODBC.con!rstCheck.Set!APPROVAL_FLAG(V.Local.sFlag)
		F.ODBC.con!rstCheck.Set!FILLER("")
		F.ODBC.con!rstCheck.Update
	F.Intrinsic.Control.Else
		F.ODBC.con!rstCheck.AddNew
		F.ODBC.con!rstCheck.Set!ORDER_NO(V.Local.sOrdNumb)
		F.ODBC.con!rstCheck.Set!LINE(V.Local.sLine)
		F.ODBC.con!rstCheck.Set!APPROVAL_FLAG(V.Local.sFlag)
		F.ODBC.con!rstCheck.Set!FILLER("")
		F.ODBC.con!rstCheck.Update	
	F.Intrinsic.Control.EndIf
	
	'We clear the checkbox and lock the Gen WO button for the next line
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.000061, "LOCK", 1)
	V.Passed.GAB-CHK-1.Set("N")
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: GCG_5108_SOLnApproval.g2u{0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(unload)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndTry
Program.Sub.SaveEntry.End

Program.Sub.Comments.Start
${$0$}$Sales Order Line Approval$}$SISCO$}$11/22/2016 3:59:52 PM$}$False
${$3$}$0$}$$}$0$}$-1$}$$}$12:00:00 AM$}$This project was designed and coded by team SkyNet on November 22 2016
This project will add a custom checkbox to the Sales Order lines screen that will manipulate whether or not the Gen WO will be available for the SO line.

Program.Sub.Comments.End